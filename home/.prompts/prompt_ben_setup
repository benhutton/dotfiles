function prompt_ben_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_ben_colors[1]}%b%f%u%c${_prompt_ben_colors[4]}●%f)"
  else
    branch_format="(${_prompt_ben_colors[1]}%b%f%u%c)"
  fi
 
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
 
  vcs_info 'prompt'
}
 
function prompt_ben_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
 
  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info
 
  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_ben_precmd
 
  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_ben_colors=(
      "%F{81}"  # Turquoise
      "%F{75}"  # Pastel Cyan
      "%F{215}" # Light Orange
      "%F{166}" # Orange - not used
      "%F{119}" # Pastel Limegreen
    )
  else
    _prompt_ben_colors=(
      "%F{cyan}"
      "%F{magenta}"
      "%F{yellow}"
      "%F{red}"
      "%F{green}"
    )
  fi
 
  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_ben_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_ben_colors[5]}%a%f)"
  local unstaged_format="${_prompt_ben_colors[2]}●%f"
  local staged_format="${_prompt_ben_colors[5]}●%f"
 
  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""
 
  local _prompt_ben_name="${_prompt_ben_colors[3]}${${(%):-@%n}#@ben}%f"
  local _prompt_ben_host="${_prompt_ben_colors[2]}${${(%):-@%m}#@ben}%f"
 
  # Define prompts.
  PROMPT="${_prompt_ben_name}${_prompt_ben_host} ${_prompt_ben_colors[5]}%~%f "'${vcs_info_msg_0_}'"» "
  RPROMPT='%F{blue}%*'
}
 
prompt_ben_setup "$@"
